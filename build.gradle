plugins {
    id 'application'
}

group = 'com.slipplus'
version = '0.1.0'

repositories {
    mavenCentral()
}

def javafxVersion = "21.0.2"
def os = org.gradle.internal.os.OperatingSystem.current()
def platform = os.isWindows() ? "win" : os.isMacOsX() ? "mac" : "linux"

dependencies {
    implementation "org.openjfx:javafx-base:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-controls:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-graphics:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-fxml:${javafxVersion}:${platform}"

    implementation 'org.apache.pdfbox:pdfbox:2.0.29'

    // JSON library for saving/loading parties + slips
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.18.0'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.18.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.18.0'
}


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = "com.slipplus.Main"
}

run {
    // Cache JVM args
    jvmArgs = [
        "--module-path", configurations.runtimeClasspath.asPath,
        "--add-modules", "javafx.controls,javafx.fxml,javafx.graphics",
        "--add-opens=javafx.graphics/javafx.scene=ALL-UNNAMED"
    ]
}

// Add this for faster builds
tasks.withType(JavaCompile) {
    options.incremental = true
    options.fork = true
}

jar {
    manifest {
        attributes(
                "Main-Class": "com.slipplus.Main"
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task createRuntimeWithJmods(type: Exec) {
    def javaHome = "C:/Program Files/Java/jdk-21.0.9"
    def javafxJmods = "C:/Program Files/Java/javafx-jmods-21.0.9"
    
    commandLine = [
            "${javaHome}/bin/jlink",
            "--module-path", "${javaHome}/jmods;${javafxJmods}",
            "--add-modules", "java.base,java.desktop,java.logging,java.xml,java.prefs,java.datatransfer,javafx.controls,javafx.fxml,javafx.graphics,javafx.base",
            "--output", "runtime-jmods",
            "--compress=2",
            "--no-header-files",
            "--no-man-pages"
    ]
}

task createExeWithJmods(type: Exec) {
    dependsOn build, createRuntimeWithJmods

    def appName = "SlipPlus"
    def mainClass = "com.slipplus.Main"

    commandLine = [
            "C:/Program Files/Java/jdk-21.0.9/bin/jpackage",
            "--type", "exe",
            "--input", "build/libs",
            "--dest", "dist",
            "--name", appName,
            "--main-jar", jar.archiveFileName.get(),
            "--main-class", mainClass,
            "--runtime-image", "runtime-jmods",
            "--icon", "src/main/resources/logos/logo.ico",
            "--app-version", "1.0.0",
            "--vendor", "SlipPlus",
            "--description", "SlipPlus Application",
            "--win-dir-chooser",  // Let user choose install directory
            "--win-menu",         // Add to start menu
            "--win-shortcut",     // Create desktop shortcut
            "--java-options", "--add-opens=javafx.graphics/javafx.scene=ALL-UNNAMED",
            "--java-options", "-Dprism.order=sw",
            "--java-options", "-Dprism.verbose=true",
            "--java-options", "-Djava.awt.headless=false",
            "--win-console"
    ]
}

